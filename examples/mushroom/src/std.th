include core video bool nat int

for_all a b c define flip of_type fn fn a fn b c fn b fn a c as
    lambda f lambda a lambda b f b a

for_all a b c define compose of_type fn fn b c fn fn a b fn a c as
    lambda g lambda f lambda x g f x

for_all a define repeat of_type fn nat fn fn a a fn a a as 
    lambda count lambda f lambda x match count with
        one    to f x
        succ new_count to f repeat a new_count f x

for_all a b define apply of_type fn fn a b fn a b as 
    lambda f lambda x f x

for_all a b define const of_type fn a fn b a as lambda x lambda _ x

type direction contains
    north
    south
    east
    west

define flip_over_y_visible of_type fn visible_character visible_character as 
    lambda c match c with
        single_quote       to grave_accent
        grave_accent       to single_quote
        open_paranthesis   to close_paranthesis
        close_paranthesis  to open_paranthesis
        comma              to period
        slash              to backslash
        backslash          to slash
        less_than          to greater_than
        greater_than       to less_than
        opening_bracket    to closing_bracket
        closing_bracket    to opening_bracket
        opening_brace      to closing_brace
        closing_brace      to opening_brace
        semicolon          to colon
        lowercase_d        to lowercase_b
        lowercase_b        to lowercase_d
        lowercase_p        to lowercase_q
        lowercase_q        to lowercase_p
        _                  to c

define flip_over_y_char of_type fn character character as 
    map_over_char flip_over_y_visible

define map_over_char of_type 
    fn fn visible_character visible_character fn character character
as lambda f lambda x match x with
    space         to space
    char c color  to char f c color

define sum_coordinate of_type fn coordinate fn coordinate coordinate as
    lambda coordinate x_i y_i lambda coordinate x_ii y_ii
        coordinate sum_int x_i x_ii sum_int y_i y_ii

define increment_whole of_type fn whole nat as lambda x match x with
    whole x     to succ x
    whole_zero  to one

define decrement_whole of_type fn whole int as lambda x match x with
    whole succ x  to pos x
    whole one     to zero
    whole_zero    to neg one

define recolor_character of_type fn color fn character character as
    lambda color lambda character match character with
        char a _  to char a color
        space     to space

define eq_coordinate of_type fn coordinate fn coordinate bool as
    lambda coordinate a b lambda coordinate c d and eq_int a c eq_int b d

define next_color of_type fn color color as lambda x match x with
    red to magenta
    magenta to blue
    blue to cyan
    cyan to green
    green to yellow
    yellow to red

define complementary of_type fn color color as 
    lambda x next_color next_color next_color x

type option_nat contains
    some_nat nat
    none_nat

type option_whole contains
    some_whole whole
    none_whole

type whole contains
    whole_zero
    whole nat

define subtract_exactly of_type fn whole fn whole option_whole as
    lambda x lambda y match x with
        whole a to match y with
            whole b     to subtract_exactly
                match a with
                    succ n  to whole n
                    one     to whole_zero
                match b with
                    succ n  to whole n
                    one     to whole_zero
            whole_zero  to some_whole whole a
        whole_zero to match y with
            whole _     to none_whole
            whole_zero  to some_whole whole_zero

define divide_exactly of_type fn whole fn whole option_whole as
    lambda x match x with
        whole _     to lambda y match subtract_exactly x y with
            some_whole a  to map_option_whole
                lambda x whole increment_whole x
                divide_exactly a y
            none_whole    to none_whole
        whole_zero  to lambda _ some_whole whole_zero

define map_option_whole of_type fn fn whole whole fn option_whole option_whole as
    lambda f lambda x match x with
        some_whole a  to some_whole f a
        none_whole    to none_whole
        
type list_whole contains
    cons_whole whole list_whole
    nil_whole

type option_list_whole contains
    some_list_whole list_whole
    none_list_whole

define map_option_list_whole of_type 
    fn 
        fn list_whole list_whole 
        fn option_list_whole option_list_whole 
as
    lambda f lambda x match x with
        some_list_whole a  to some_list_whole f a
        none_list_whole    to none_list_whole

define divisible of_type fn whole fn whole bool as
    lambda x lambda y match divide_exactly x y with
        some_whole _  to true
        none_whole    to false

define prime_factors of_type fn nat fn whole list_whole as
    lambda x lambda y match y with
        whole aaa to match aaa with
            one     to nil_whole 
            succ _  to match divide_exactly y whole x with
                some_whole a  to cons_whole whole x prime_factors x a
                none_whole    to prime_factors succ x y
        whole_zero to nil_whole 

define uwulayer_frames of_type fn bad_frame fn bad_frame bad_frame as
    lambda top lambda bottom match top with
        unsafe_cons_cell head tail  to unsafe_cons_cell head uwulayer_frames tail bottom
        empty_frame                 to bottom

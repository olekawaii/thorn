include core std


-- types -----------------------------------------------------------------------


type video contains
    single frame
    prepend frame video

type frame contains
    frame column column

type column contains
    empty_column
    cons_column horizontal column

type horizontal contains
    horizontal row row

type row contains
    empty_row
    cons_row grid_cell row

type grid_cell contains
    empty_grid_cell
    full_grid_cell character
    

-- video functions -------------------------------------------------------------


define head of_type fn video frame as lambda x match x with
    single f     to f
    prepend f _  to f

-- apply a fn frame frame function to every frame in a video

define entirely of_type fn fn frame frame fn video video as 
    lambda f lambda v match v with
        single head        to single f head
        prepend head tail  to prepend f head entirely f tail

define video_length of_type fn video nat as
    lambda x match x with
        prepend _ tail  to succ video_length tail
        single _        to one

-- layer a b layers a on top of b while also preserving the loopingness. 
-- layering a video with 2 frames and one with 3 results in a video with
-- 6 frames

define layer of_type fn video fn video video as
    lambda t lambda b take 
        lcm video_length t video_length b 
        layer_helper loop_video_forever t loop_video_forever b

define reverse of_type fn video video as
    lambda x match x with
        prepend a b  to sequence reverse b single a
        single a     to single a

-- make the video x loop by adding a cropped reversed x to the end of it

define loop of_type fn video video as lambda x match x with
    single _ to x
    prepend heada taila to match reverse taila with
        single headb     to prepend heada single headb
        prepend _ tailb  to sequence x tailb

-- duplicate the last frame

define extend of_type fn video video as lambda x append head reverse x x

-- sequence two videos one after the other

define sequence of_type fn video fn video video as
    lambda x lambda y match x with
        prepend head tail  to prepend head sequence tail y
        single head        to prepend head y

define take of_type fn nat fn video video as
    lambda counter lambda bad_frames match bad_frames with
        single head        to single head
        prepend head tail  to match counter with
            succ a  to prepend head take a tail
            one     to single head

-- slow n multiplies every frame n times

define slow of_type fn nat fn video video as
    lambda num lambda vid match vid with
        single head        to take num loop_video_forever single head
        prepend head tail  to sequence
            take num loop_video_forever single head
            slow num tail

-- for n f v applies f to v n times

define for of_type fn nat fn fn video video fn video video as repeat video

define loop_video_forever of_type fn video video as
    lambda x sequence x loop_video_forever x

-- push the first frame to the back

define rotate of_type fn video video as
    lambda vid match vid with
        prepend head tail  to sequence tail single head
        single head        to single head

define append of_type fn frame fn video video as 
    lambda x flip video video video sequence single x

define rotate_right of_type fn video video as lambda x reverse rotate reverse x

-- continuously f applies the function f to every frame n times where n is
-- the index of the frame

define continuously of_type fn fn frame frame fn video video as 
    lambda f lambda v match entirely f v with
        single head       to single head
        prepend head tail to prepend head continuously f tail

-- frame functions -------------------------------------------------------------


-- same as for but for frames

define by of_type fn nat fn fn frame frame fn frame frame as repeat frame

-- shift_by x y shifts the frame by the x and y values

for_all a define identity of_type fn a a as lambda x x

define shift_by of_type fn int fn int fn frame frame as 
    lambda x lambda y lambda f match of_type fn frame frame match x with
            pos a  to by a shift east
            neg a  to by a shift west
            zero   to identity frame
        with
            fa to match of_type fn frame frame match y with 
                    pos a to by a shift north
                    neg a to by a shift south
                    zero  to identity frame
                with
                    fb to fa fb f

-- insert x y c inserts the character c at the coordinate x y

define insert of_type fn int fn int fn character fn frame frame as
    lambda x lambda y lambda chr
        lambda frame col_neg col_pos match y with
            neg index to frame
                insert_column x index chr col_neg
                col_pos
            pos index to frame
                col_neg
                insert_column x succ index chr col_pos
            zero to frame
                col_neg
                insert_column x one chr col_pos

-- layer_frames a b layers a on top of b

define layer_frames of_type fn frame fn frame frame as
    lambda frame na pa lambda frame nb pb
        frame
            layer_columns na nb
            layer_columns pa pb

define new_frame of_type frame as
    frame empty_column empty_column

-- shift d shifts the coordinates of the frame in the direction d

define shift of_type fn direction fn frame frame as
    lambda dir lambda f match f with
        frame col_neg col_pos to match dir with
            north to match col_neg with
                cons_column hor col_succ to frame
                    col_succ
                    cons_column hor col_pos
                empty_column to frame
                    empty_column
                    cons_column empty_horizontal col_pos
            south to match col_pos with
                cons_column hor col_succ to frame
                    cons_column hor col_neg
                    col_succ
                empty_column to frame
                    cons_column empty_horizontal col_neg
                    empty_column
            east to map_columns shift_horizontal right f
            west to map_columns shift_horizontal left f

-- essentially multiply all x coordinates by -1

define flip_over_y of_type fn frame frame as map_columns over_y_hor


-- misc ------------------------------------------------------------------------


define empty_horizontal of_type horizontal as
    horizontal empty_row empty_row

define insert_horizontal of_type fn int fn character fn horizontal horizontal as
    lambda index lambda chr lambda horizontal n p
        match index with
            neg index  to horizontal insert_row index chr n p
            pos index  to horizontal n insert_row succ index chr p
            zero       to horizontal n insert_row one chr p

define insert_row of_type fn nat fn character fn row row as
    lambda index lambda chr lambda r match r with
        empty_row          to insert_row index chr 
            cons_row empty_grid_cell empty_row
        cons_row head tail to match index with
            one     to cons_row full_grid_cell chr tail
            succ i  to cons_row head insert_row i chr tail

define insert_column of_type fn int fn nat fn character fn column column as
    lambda x lambda y lambda chr lambda col match col with
        cons_column head tail to match y with
            succ i  to cons_column head insert_column x i chr tail
            one     to cons_column insert_horizontal x chr head tail
        empty_column to insert_column x y chr 
            cons_column empty_horizontal empty_column

define from_video of_type fn bad_video video as
    lambda v match v with
        cons_frame x xs           to prepend from_frame x from_video xs
        cons_frame x empty_video  to single from_frame x
        empty_video               to ...

define from_frame of_type fn bad_frame frame as
    lambda f match f with
        empty_frame                                  to new_frame
        unsafe_cons_cell cell coordinate x y c tail  to
            insert x y c from_frame tail

define into_video of_type fn video bad_video as lambda vid match vid with
    prepend head tail  to cons_frame into_frame head into_video tail
    single f           to cons_frame into_frame f empty_video

define into_frame of_type fn frame bad_frame as
    lambda frame neg_col pos_col
        uwulayer_frames
            column_to_frame one neg neg_col
            match pos_col with
                empty_column          to empty_frame
                cons_column head tail to uwulayer_frames
                    horizontal_to_frame zero head
                    column_to_frame one pos tail

define horizontal_to_frame of_type fn int fn horizontal bad_frame as
    lambda y lambda horizontal neg_row pos_row
        uwulayer_frames
            row_to_frame one y neg neg_row
            match pos_row with
                empty_row to empty_frame
                cons_row x xs to match of_type fn bad_frame bad_frame 
                        match x with
                            empty_grid_cell  to identity bad_frame
                            full_grid_cell a to unsafe_cons_cell cell 
                                coordinate zero y 
                                a
                    with
                        f to f row_to_frame one y pos xs

define row_to_frame of_type fn nat fn int fn fn nat int fn row bad_frame as
    lambda index lambda y lambda sign lambda r match r with
        empty_row           to empty_frame
        cons_row head tail  to
            apply bad_frame bad_frame
                match head with
                    empty_grid_cell   to identity bad_frame
                    full_grid_cell x  to
                        unsafe_cons_cell cell coordinate sign index y x
                row_to_frame succ index y sign tail

define column_to_frame of_type fn nat fn fn nat int fn column bad_frame as
    lambda index lambda sign lambda col match col with
        empty_column           to empty_frame
        cons_column head tail  to uwulayer_frames
            horizontal_to_frame sign index head
            column_to_frame succ index sign tail

type horizontal_direction contains
    right
    left

define shift_horizontal of_type
    fn horizontal_direction fn horizontal horizontal
as
    lambda dir lambda horizontal r_l r_r
        match dir with
            right to match r_l with
                cons_row head tail  to horizontal tail cons_row head r_r
                empty_row           to horizontal 
                    empty_row 
                    cons_row empty_grid_cell r_r
            left to match r_r with
                cons_row head tail  to horizontal cons_row head r_l tail
                empty_row           to horizontal 
                    cons_row empty_grid_cell r_l 
                    empty_row

define map_columns of_type fn fn horizontal horizontal fn frame frame as
    lambda f lambda frame cn cp
        frame
            map_column f cn
            map_column f cp

define map_column of_type fn fn horizontal horizontal fn column column as
    lambda f lambda col match col with
        cons_column head tail  to cons_column f head map_column f tail
        empty_column           to empty_column

type tuple_col_col contains
    tuple_col_col column column

define layer_columns of_type fn column fn column column as
    lambda ca lambda cb match tuple_col_col ca cb with
        tuple_col_col cons_column head_a tail_a cons_column head_b tail_b to
            cons_column
                layer_horizontal head_a head_b
                layer_columns tail_a tail_b
        tuple_col_col empty_column a             to a
        tuple_col_col a            empty_column  to a

define layer_horizontal of_type fn horizontal fn horizontal horizontal as
    lambda horizontal aa ba lambda horizontal ab bb
        horizontal
            layer_rows aa ab
            layer_rows ba bb

type tuple_row_row contains
    tuple_row_row row row

define layer_rows of_type fn row fn row row as
    lambda x lambda b match tuple_row_row x b with
        tuple_row_row empty_row       a               to a
        tuple_row_row a               empty_row       to a
        tuple_row_row cons_row aa ba  cons_row ab bb  to
            cons_row
                match aa with
                    empty_grid_cell   to ab
                    full_grid_cell _  to aa
                layer_rows ba bb

-- expects videos of equal length. will refactor to use infinite_video in the
-- future.

define layer_helper of_type fn video fn video video as
    lambda x lambda y match x with
        prepend head_a tail_a to match y with
            single head_b          to ...
            prepend head_b tail_b  to prepend
                layer_frames head_a head_b
                layer_helper tail_a tail_b
        single a to match y with
            single b     to single layer_frames a b
            prepend _ _  to ...

define over_y_hor of_type fn horizontal horizontal as
    lambda horizontal n p match p with
        empty_row     to horizontal 
            empty_row 
            cons_row empty_grid_cell map_row flip_over_y_char n
        cons_row h t  to horizontal 
            map_row flip_over_y_char t 
            cons_row h map_row flip_over_y_char n

define map_over_grid_cell of_type
    fn fn character character fn grid_cell grid_cell
as
    lambda f lambda x match x with
        full_grid_cell a  to full_grid_cell f a
        empty_grid_cell   to empty_grid_cell

define map_horizontal of_type
    fn fn character character fn horizontal horizontal
as
    lambda f lambda horizontal ra rb horizontal
        map_row f ra
        map_row f rb

define map_row of_type fn fn character character fn row row as
    lambda f lambda x match x with
        cons_row head tail  to cons_row map_over_grid_cell f head map_row f tail
        empty_row           to empty_row
